#% text_encoding = iso8859_1
##-----------------------------------------------------------------------------------------------
## Web Maps Connector (which shows web maps as layers in Smallworld(TM) Core Spatial Technology)
## Copyright (C) 2017 KUBRA
##
## This program is free software: you can redistribute it and/or modify
## it under the terms of the GNU General Public License as published by
## the Free Software Foundation, either version 3 of the License, or
## (at your option) any later version.
##
## This program is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with this program.  If not, see <http:##www.gnu.org/licenses/>.
##
## You may contact KUBRA at https://kubra.com/contact-us/
##-----------------------------------------------------------------------------------------------

_package user
$


def_slotted_exemplar(:wmc_geocode_result,
		    {},
		    {:wmc_rwo_record})
$

_pragma(classify_level=restricted)
_method wmc_geocode_result.dataset_name

	_return write_string(_self.class_name.write_string.split_by(%_).first,"_dataset").as_symbol()
_endmethod
$
_pragma(classify_level=advanced)
_method wmc_geocode_result.wmc_new_from(a_prop_list,unique_id)
	## 
	##

	_local fd_props << property_list.new()
	_local coll << _self.get_collection(:geocode_result)


	_for field_method _over {:physical_fields|()|,:geometry_fields|()|}.fast_elements()
	_loop
		_for a_fd _over coll.perform_iter(field_method)
		_loop
			fd_props[a_fd.name] << a_prop_list[a_fd.name]
		_endloop
	_endloop

	fd_props[:id] << write_string(date_time_now(),"-",unique_id)
	
	rt << record_transaction.new_insert(coll,fd_props)
	a_rec << rt.run()

	
	>> a_rec
_endmethod
$
_pragma(classify_level=advanced)
_method wmc_geocode_result.get_spatial_context_names()
	## get_spatial_context_names() : simple_vector
	
	_return {:gis}
_endmethod
$
_pragma(classify_level=advanced, topic=if_spatial_context, usage=subclassable)
_method wmc_geocode_result.get_spatial_context( context_name )
	##
	## Returns the spatial_context for the named context for self. 
	##

	_local result_sc << _unset
	_local a_geom
	
	_if context_name _is :gis _andif 
	    (a_geom << _self.location) _isnt _unset 
	_then
		result_sc << spatial_context.new(context_name,_self)
		result_sc.add_element(a_geom.world,geometry_set.new_with(a_geom),_unset,_unset,_self.display)
	_endif

	_return result_sc
_endmethod
$
